import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'net.nemerosa.versioning' version '2.6.1'
}

group 'fr.gravendev'
version "1.0-DEVELOP-${versioning.info.full}"
def jdaVersion = '4.0.0_39'

sourceCompatibility = 1.8

jar {
    manifest {
        attributes(
                'Main-Class': 'fr.gravendev.multibot.Main',
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Build-Revision': versioning.info.commit,
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

shadowJar {
    shadowJar.setArchiveName("MultiBot-${version}.jar")
}

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

archivesBaseName = 'MultiBot'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/resources"]
        }
    }
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    implementation group: 'net.dv8tion', name: 'JDA', version: jdaVersion
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.3.1'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.16'
    implementation group: 'net.coobird', name: 'thumbnailator', version: '0.4.8'
    implementation group: 'com.sparkjava', name: 'spark-core', version: '2.8.0'
    implementation group: 'org.json', name: 'json', version: '20190722'
    implementation group: 'com.electronwill.night-config', name: 'toml', version:'3.6.0'
}
